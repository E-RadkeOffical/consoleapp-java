plugins {
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'java'
    id 'application'
    id 'jacoco'
}

// project info

group = 'edu.luc.cs.cs313'

version = '0.1-SNAPSHOT'

description = """consoleapp"""

// compiler options

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

compileJava {
    options.compilerArgs << "-Xlint:all"
    options.encoding = 'UTF-8'
}

// run and startScript configuration

run {
    standardInput = System.in
}

libsDirName = 'lib' // simple workaround for error in startScripts task
mainClassName = 'hw.Main'

// create "fat" jar with all dependencies

jar {
    manifest {
        attributes 'Main-Class' : mainClassName
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

// dependencies

repositories {
    jcenter()
}

dependencies {
    implementation group: 'org.apache.commons', name: 'commons-collections4', version:'4.4'
    implementation group: 'commons-cli', name: 'commons-cli', version:'1.4'
    testImplementation group: 'junit', name: 'junit', version:'4.13'
}
